apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'

version = "1.0.$System.env.GO_PIPELINE_COUNTER"

modifyPom {
    project {
        groupId 'com.thoughtworks.gauge'
        artifactId 'gauge-api-client'
        name 'Gauge API Client'
        description 'Java stubs for gauge-api'
        url 'https://github.com/getgauge/gauge-proto'


        scm {
            url 'https://github.com/getgauge/gauge-proto'
            connection 'scm:https://github.com/getgauge/gauge-proto.git'
            developerConnection 'scm:git://github.com/getgauge/gauge-proto.git'
        }

        licenses {
            license {
                name 'GNU General Public License, Version 3.0'
                url 'https://www.gnu.org/licenses/gpl.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'getgauge'
                name 'Gauge Team'
                email 'getgauge@googlegroups.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

nexusStaging {
    packageGroup = "com.thoughtworks.gauge"
}

repositories {
    maven { url "https://plugins.gradle.org/m2/" }
}

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0"
    }
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:3.5.1'
    compile 'io.grpc:grpc-stub:1.11.0'
    compile 'io.grpc:grpc-protobuf:1.11.0'
    if (JavaVersion.current().isJava9Compatible()) {
        compile 'javax.annotation:javax.annotation-api:1.3.2'
    }
    protobuf files("api.proto", "spec.proto", "lsp.proto", "messages.proto")
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1-1'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.0-pre2'
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            grpc {}
        }
    }
}
